---
page_title: "hcp_azure_peering_connection Resource - terraform-provider-hcp"
subcategory: ""
description: |-
  The Azure peering connection resource allows you to manage a peering connection between an HVN and a peer Azure VNet.
---

# hcp_azure_peering_connection (Resource)

-> **Note:** This resource is currently in internal preview only.

The Azure peering connection resource allows you to manage a peering connection between an HVN and a peer Azure VNet.

## Example Usage

```terraform
resource "hcp_hvn" "hvn" {
  hvn_id         = "main-hvn"
  cloud_provider = "azure"
  region         = "westus2"
  cidr_block     = "172.25.16.0/20"
}

resource "hcp_azure_peering_connection" "peer" {
  hvn_link                 = hcp_hvn.hvn.self_link
  peering_id               = "dev"
  peer_vnet_name           = azurerm_virtual_network.vnet.name
  peer_subscription_id     = "subscription-uuid"
  peer_tenant_id           = "tenant-uuid"
  peer_resource_group_name = azurerm_resource_group.rg.name
  peer_vnet_region         = azure_rm_virtual_network.region
}

resource "hcp_peering_connection_activation" "activation" {
  peering_id = hcp_azure_peering_connection.peer.peering_id
  hvn_link   = hcp_hvn.hvn.self_link
}

resource "hcp_hvn_route" "route" {
  hvn_link         = hcp_hvn.hvn.self_link
  hvn_route_id     = "azure-route"
  destination_cidr = "172.31.0.0/16"
  target_link      = hcp_azure_peering_connection.peer.self_link
}

provider "azurerm" {
  features {}
}

resource "azurerm_resource_group" "rg" {
  name     = "resource-group-test"
  location = "West US"
}

resource "azurerm_virtual_network" "vnet" {
  name                = "vnet-test"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  address_space = [
    "10.0.0.0/16"
  ]
}

resource "azuread_service_principal" "principal" {
  application_id = hcp_azure_peering_connection.peer.application_id
}

resource "azurerm_role_definition" "definition" {
  name  = "hcp-hvn-peering-access"
  scope = azurerm_virtual_network.vnet.id

  assignable_scopes = [
    azurerm_virtual_network.vnet.id
  ]

  permissions {
    actions = [
      "Microsoft.Network/virtualNetworks/peer/action",
      "Microsoft.Network/virtualNetworks/virtualNetworkPeerings/write"
    ]
  }
}

resource "azurerm_role_assignment" "assignment" {
  principal_id       = azuread_service_principal.principal.id
  scope              = azurerm_virtual_network.vnet.id
  role_definition_id = azurerm_role_definition.definition.role_definition_resource_id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- **hvn_link** (String) The `self_link` of the HashiCorp Virtual Network (HVN).
- **peer_resource_group_name** (String) The resource group name of the peer VNet in Azure.
- **peer_subscription_id** (String) The subscription ID of the peer VNet in Azure.
- **peer_tenant_id** (String) The tenant ID of the peer VNet in Azure.
- **peer_vnet_name** (String) The name of the peer VNet in Azure.
- **peer_vnet_region** (String) The region of the peer VNet in Azure.
- **peering_id** (String) The ID of the peering connection.

### Optional

- **id** (String) The ID of this resource.
- **timeouts** (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))

### Read-Only

- **application_id** (String) The application ID of the HCP VNet backing the HVN.
- **created_at** (String) The time that the peering connection was created.
- **expires_at** (String) The time after which the peering connection will be considered expired if it hasn't transitioned into `ACCEPTED` or `ACTIVE` state.
- **organization_id** (String) The ID of the HCP organization where the peering connection is located. Always matches the HVN's organization.
- **project_id** (String) The ID of the HCP project where the peering connection is located. Always matches the HVN's project.
- **provider_peering_id** (String) The peering connection ID used by Azure.
- **self_link** (String) A unique URL identifying the peering connection.

<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- **create** (String)
- **default** (String)
- **delete** (String)

## Import

Import is supported using the following syntax:

```shell
# The import ID is {hvn_id}:{peering_id}
terraform import hcp_azure_peering_connection.peer main-hvn:199e7e96-4d5f-4456-91f3-b6cc71f1e561
```
